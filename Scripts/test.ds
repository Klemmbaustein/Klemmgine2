using engine
using engine::input
using system

module game

class MyObject : SceneObject
{
	[Export name="hello"]
	AssetRef testModel = emptyAsset("kmdl")

	MoveComponent movement = new()
	CameraComponent camera = new()

	float lastY = 0

	bool isSliding = false
	float slideTimer = 0

	override fn begin()
	{
		MeshComponent testMesh = new()
		testMesh.load(testModel)
		attach(testMesh)
		testMesh.scale = new(0.5, 1, 0.5)

		attach(camera)
		attach(movement)
		camera.use()
		camera.setFOV(80)
		camera.position = new(0, 0.8, 0)

		movement.gravity = 15
		movement.maxSpeed = 5

		lastY = this.position.y

		Map<string, string> myMap = new()

		myMap.insert("hi", "hello")

		myMap.remove("hi")
	}

	override fn update()
	{
		Rotation3 moveRotation = new(0, this.rotation.y, 0)

		if isKeyDown(Key::w)
		{
			movement.addInput(forward(moveRotation))
		}
		if isKeyDown(Key::s)
		{
			movement.addInput(-forward(moveRotation))
		}
		if isKeyDown(Key::d)
		{
			movement.addInput(right(moveRotation))
		}
		if isKeyDown(Key::a)
		{
			movement.addInput(-right(moveRotation))
		}

		if isKeyDown(Key::space)
		{
			movement.jump()
		}

		Vector2 mouseMovement = getMouseMovement()

		rotation.y -= mouseMovement.x
		camera.rotation.p -= mouseMovement.y

		camera.rotation.p = math::clampF(camera.rotation.p, -90.0, 90.0)

		if isKeyDown(Key::shift)
		{
			if not isSliding
			{
				beginSlide()
			}
			isSliding = true
		}
		else
		{
			if isSliding
			{
				endSlide()
			}
			isSliding = false
		}

		if isSliding
		{
			updateSlide()
		}
		lastY = this.position.y
	}

	fn updateSlide()
	{
		if not movement.isOnGround()
		{
			return
		}

		float difference = (this.position.y - lastY) / getDelta()

		movement.maxSpeed = 8 - difference
		movement.deceleration = difference + slideTimer * 8
		slideTimer += (1 + difference) * getDelta()
		if slideTimer < 0
		{
			slideTimer = 0
		}
	}

	fn beginSlide()
	{
		if movement.isOnGround()
		{
			movement.setVelocity(movement.getVelocity() * new Vector3(2, 1, 2))
		}
		movement.acceleration = 5
		movement.canMoveUpSlopes = true
		slideTimer = 0
		camera.position.y = 0
	}

	fn endSlide()
	{
		movement.acceleration = 30
		movement.deceleration = 40
		movement.canMoveUpSlopes = false
		camera.position.y = 0.8
	}
}
